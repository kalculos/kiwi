plugins {
    id 'java'
    id("me.him188.maven-central-publish") version "1.0.0-dev-3"
}
apply plugin: 'maven-publish'
apply plugin: 'signing'

group property("project.group")
version property("project.version")

subprojects {
    apply plugin: 'java'
    apply plugin: 'me.him188.maven-central-publish'
    apply plugin: 'maven-publish'

    project.version = parent.version
    project.group = parent.group

    mavenCentralPublish {
        useCentralS01()
        projectName = "kiwi"
        artifactId = project.name
        groupId = project.group
        description("Kiwi is a collection of useful utilities for Java.")
        projectUrl("https://github.com/kalculos/Kiwi")
        workingDir = file("/tmp/a")

        connection = "scm:git:git://github.com/kalculos/Kiwi"
        licenseMit()

        developer("iceBear67")
    }
    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:23.0.0'
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }
    test {
        useJUnitPlatform()
    }
    def targetJavaVersion = Integer.parseInt(property("java.version"))
    tasks.withType(JavaCompile).configureEach {
        // ensure that the encoding is set to UTF-8, no matter what the system default is
        // this fixes some edge cases with special characters not displaying correctly
        // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
        // If Javadoc is generated, this must be specified in that task too.
        it.options.encoding = "UTF-8"
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            it.options.release.set(targetJavaVersion)
        }
    }
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()
    }
}